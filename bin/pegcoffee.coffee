#!/usr/bin/env node

util = require 'util'
fs   = require 'fs'
PEG  = require 'pegjs'
PEGjsCoffeePlugin = require '../lib/peg-coffee.js'

# Helpers


printVersion = -> util.puts("PEG.js CoffeeScript Plugin #{PEGjsCoffeePlugin.VERSION}")


printHelp = ->
  util.puts """
    Usage: pegcoffee [options] [--] [<input_file>] [<output_file>]

    Generates a parser from the PEG grammar specified in the <input_file> and
    writes it to the <output_file>.
    
    If the <output_file> is omitted, its name is generated by changing the
    <input_file> extension to \".js\". If both <input_file> and <output_file> are
    omitted, standard input and output are used.
    
    Options:
      -e, --export-var <variable>  name of the variable where the parser object
                                   will be stored (default: \"module.exports\")
          --cache                  make generated parser cache results
          --track-line-and-column  make generated parser track line and column
          --js                     use plain javascript in actions
      -v, --version                print version information and exit
      -h, --help                   print help and exit
  """

exitSuccess = -> process.exit(0)

exitFailure = -> process.exit(1)

abort = (message) ->
  util.error message
  exitFailure()

# Arguments

#Trim "node" and the script path.
args = process.argv.slice 2

isOption = (arg) -> /^-/.test(arg)

nextArg = -> args.shift()

# Files 

readStream = (inputStream, callback) ->
  input = ""
  inputStream.on("data", (data) -> input += data )
  inputStream.on("end", -> callback input )


# Main 

# This makes the generated parser a CommonJS module by default.
exportVar = "module.exports"
# Set the usage of CoffeeScript as default
options = 
  cache:              false
  trackLineAndColumn: false
  js:                 false


while args.length > 0 and isOption args[0]
  switch (args[0])
    when "-e", "--export-var"
      nextArg()
      if args.length is 0
        abort "Missing parameter of the -e/--export-var option."
      exportVar = args[0]
      
    when "--js"
      options.js = true
      
    when "--cache"
      options.cache = true

    when "--track-line-and-column"
      options.trackLineAndColumn = true

    when "-v","--version"
      printVersion()
      exitSuccess()

    when "-h", "--help"
      printHelp()
      exitSuccess()

    when "--"
      nextArg()

    else abort "Unknown option: #{args[0]}."
  nextArg()

switch args.length
  when 0
    process.stdin.resume()
    inputStream = process.stdin
    outputStream = process.stdout

  when 1, 2
    inputFile = args[0]
    inputStream = fs.createReadStream inputFile
    inputStream.on "error", ->
      abort "Can't read from file \"#{inputFile}\"."

    outputFile =
      if args.length is 1 then args[0].replace(/\.[^.]*$/, ".js") else args[1]
    outputStream = fs.createWriteStream outputFile
    outputStream.on "error", ->
      abort "Can't write to file \"#{outputFile}\"."
  else abort "Too many arguments."


readStream inputStream, (input) ->
  PEGjsCoffeePlugin.addTo PEG unless options.js
  try 
    parser = PEG.buildParser(input, options)
  catch e
    if e.line? and e.column?
      abort "#{e.line}:#{e.column}:#{e.message}"
    else
      abort e.message

  outputStream.write "#{exportVar} = #{parser.toSource()};\n"
  outputStream.end() if outputStream isnt process.stdout
